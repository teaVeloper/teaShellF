# ==========================================================================
#                                   _                                      |
#                                  | |                                     |
#                           _______| |__  _ __ ___                         |
#                          |_  / __| '_ \| '__/ __|                        |
#                           / /\__ \ | | | | | (__                         |
#                          /___|___/_| |_|_|  \___|                        |
#                                                                          |
# ==========================================================================

# If this isn't an interactive shell, stop here.
[[ -o interactive ]] || return

# ---- Powerlevel10k instant prompt (keep at top) ----
local instant_prompt_file="${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh"
[[ -r "$instant_prompt_file" ]] && source "$instant_prompt_file"

local p10k_config="${ZDOTDIR}/p10k.zsh"
[[ -r "$p10k_config" ]] && source "$p10k_config"

# ---- Antidote (optional) ----
# Root name of the plugins files (.txt and .zsh) antidote will use.
local zsh_plugins="${ZDOTDIR}/.zsh_plugins"

# set of variables used/needed by plugins
export _Z_DATA="$XDG_DATA_HOME/z/data"

# Ensure the plugin list exists.
[[ -f "${zsh_plugins}.txt" ]] || : >| "${zsh_plugins}.txt"

# Locate Antidote.
local arch_antidote="/usr/share/zsh-antidote"
local git_antidote="${XDG_DATA_HOME}/antidote"

local ANTIDOTE_DIR=""
if [[ -d "$arch_antidote" ]]; then
  ANTIDOTE_DIR="$arch_antidote"
elif [[ -d "$git_antidote" ]]; then
  ANTIDOTE_DIR="$git_antidote"
fi

if [[ -n "$ANTIDOTE_DIR" ]]; then
  # Lazy-load antidote from its functions directory.
  fpath=("${ANTIDOTE_DIR}/functions" $fpath)
  autoload -Uz antidote

  # Generate a new static bundle when .txt changes.
  if [[ ! "${zsh_plugins}.zsh" -nt "${zsh_plugins}.txt" ]]; then
    antidote bundle <"${zsh_plugins}.txt" >| "${zsh_plugins}.zsh"
  fi

  [[ -r "${zsh_plugins}.zsh" ]] && source "${zsh_plugins}.zsh"
else
  print -r -- "antidote not found; skipping plugin load" 1>&2
fi

unset arch_antidote git_antidote ANTIDOTE_DIR

# ---- Small interactive env knobs (only if used) ----
if command -v xclip >/dev/null 2>&1; then
  export FORGIT_COPY_CMD='xclip -selection clipboard'
fi

# Spark (optional)
if [[ -n "${SPARK_HOME:-}" && -d "$SPARK_HOME/bin" ]]; then
  path+=("$SPARK_HOME/bin")
fi

# ---- mise (keep late) ----
if command -v mise >/dev/null 2>&1; then
  eval "$(mise activate zsh)"
fi
